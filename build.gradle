plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id "com.google.protobuf" version "0.9.4"
}

ext {
	lombokVersion = '1.18.32'
	mapstructVersion = '1.6.0.Beta1'
	grpcVersion = '1.62.2'
	protobufVersion = '3.25.2'
	testContainersVersion = '1.19.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	// For reactive web applications
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Spring Data R2DBC
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

	// R2DBC PostgreSQL Driver
	implementation 'io.r2dbc:r2dbc-postgresql:0.8.13.RELEASE'

	// Liquibase
	implementation "org.liquibase:liquibase-core"
	runtimeOnly 'org.springframework:spring-jdbc'
	runtimeOnly 'org.postgresql:postgresql'

	// Mapstruct
	implementation "org.mapstruct:mapstruct:$mapstructVersion"
	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	// Lombok
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	annotationProcessor "org.projectlombok:lombok:$lombokVersion"

	// Mapstruct-lombok binding
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

	// gRPC dependencies
	implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
	implementation "io.grpc:grpc-protobuf:$grpcVersion"
	implementation "io.grpc:grpc-stub:$grpcVersion"
	implementation 'net.devh:grpc-server-spring-boot-starter:3.0.0.RELEASE'

	// For correct gRPC work
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	// Test dependencies

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "io.grpc:grpc-testing:$grpcVersion"
	testImplementation 'io.projectreactor:reactor-test:3.2.3.RELEASE'

	// Testcontainers
	testImplementation "org.testcontainers:postgresql:$testContainersVersion"
	testImplementation "org.testcontainers:r2dbc:$testContainersVersion"
	testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"

//	// Citrus Testing Framework
//	implementation 'org.citrusframework:citrus-base:4.2.0'
//	testImplementation 'org.citrusframework:citrus-jms:4.2.0'
//	implementation 'org.citrusframework:citrus-junit5:4.2.0'

}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:$protobufVersion"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
